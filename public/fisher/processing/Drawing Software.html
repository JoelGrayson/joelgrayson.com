<!DOCTYPE html>
<!-- 11.6.20 -->
<html>
    <head>
        <title>Joel's Drawing Software</title>
        <script src="https://cdnjs.cloudflare.com/ajax/libs/processing.js/1.6.0/processing.min.js"></script>
    </head>
    <style>
        body {
            padding: 0;
            margin: 0;
        }
    </style>
    <body>
        <script type="application/processing">
/*------------------------------------------------\
|                TABLE OF CONTENTS:               |
|   Variables ………………………………………………………………………… 40     |
|     Color ……………………………………………………………………………… 41     |
|     Controls ……………………………………………………………………… 47     |
|     Tool Icons ………………………………………………………………… 61     |
|   Setup …………………………………………………………………………………… 70     |
|      Color …………………………………………………………………………… 71     |
|      Preferences …………………………………………………………… 79     |
|      Controls …………………………………………………………………… 85     |
|      Frame Counters …………………………………………………… 93     |
|    Draw …………………………………………………………………………………… 99     |
|      Clear Background ……………………………………………… 100    |
|      Tool Type ………………………………………………………………… 104    |
|      Drawing ……………………………………………………………………… 123    |
|      Expanded Menu Bar …………………………………………… 127    |
|        Dropdown Menu for Color & Size …… 140    |
|          Base Rectangle ………………………………………… 141    |
|          Expanded Rectangle ……………………………… 150    |
|            Color Options ……………………………………… 154    |
|            Highlights on Hover ……………………… 161    |
|            Size Options ………………………………………… 194    |
|        Notification …………………………………………………… 215    |
|      Contracted Menu Bar ……………………………………… 242    |
|      Frame Counters & Toggle Variables … 254    |
|    mousePressed ……………………………………………………………… 264    |
|      Expanded Menu ……………………………………………………… 266    |
|         Menu Bar (not contracted) ……………… 268    |
|           Selecting Tool ……………………………………… 275    |
|           Open/Close Dropdown Menu …………… 281    |
|           Dropdown Menu Option Selected… 295    |
|             Change Color/Size ………………………… 297    |
|             Closing Procedures ……………………… 320    |
|           Dismiss Notification [x] …………… 325    |
|         Contracted Menu Bar ……………………………… 330    |
|    keyPressed (keyboard shortcuts)……………… 337    |
|                                                 |
\------------------------------------------------*/

//VARIABLES
//Color
color red;
color green;
color blue;
color brushColor;
color backgroundColor;
//Controls
float brushSize;
float notificationX; //notification X axis
String toolType; //"brush" or "eraser"
boolean contractMenu;
boolean dropdownMenu; //opens dropdownMenu & closes dropdownMenu w/ resetSettingsMenu
boolean clear;
boolean resetMenu; //paints over menu when contracted
boolean resetSettingsMenu; //closes dropdownMenu w/ help of dropdownMenu = false
boolean notificationClosed;
int framesAfterStart;
int dontDrawForXFrames; //When over 0, cannot draw. When user selects new setting, or clicks in the setting menu bar, pen will not draw
int framesAfterNotificationClosed;
int framesAfterClear; //makes clearImage blue for a second
//Tool Icons
PImage brush;
PImage eraser;
PImage clearImage;
//Blue tool icons
PImage brushBlue;
PImage eraserBlue;
PImage clearBlue;

void setup() {  
  //COLOR palette
  red=color(247,47,32);
  green=color(52,237,83);
  blue=color(9,40,185);
  brushColor=blue;
  backgroundColor=color(255,255,255);
  background(backgroundColor);
  
  //PREFERENCES
  brushSize=22;
  frameRate(100); //recommended not to change
  size(700,700);
  textSize(30);
  
  //CONTROLS
  brush = loadImage("http://localhost:3000/fisher/processing/images/drawing-software/brush.png");
  brushBlue = loadImage("/fisher/processing/images/drawing-software/brushBlue.png"); //blue brush
  eraser = loadImage("/fisher/processing/images/drawing-software/eraser.png");
  eraserBlue = loadImage("/fisher/processing/images/drawing-software/eraserBlue.png"); //black eraser
  clearImage = loadImage("/fisher/processing/images/drawing-software/clear.png");
  clearBlue=loadImage("/fisher/processing/images/drawing-software/clearBlue.png");
  toolType="brush";
  dropdownMenu = false; 
  notificationClosed = false;
  
  //FRAME COUNTERS (these variables act like stopwatches/timers afters different events)
  dontDrawForXFrames=-1000; //when >0, doesn't draw
  framesAfterClear=1000; //when <80, clear icon is blue
  framesAfterNotificationClosed=1000;
}

void draw() {
  //CLEAR BACKGROUND
  if (clear) { //only occurs if button pressed
    background(backgroundColor);
  }
  //TOOL TYPE
  if (toolType=="brush") {
    fill(brushColor);
    stroke(brushColor);
    strokeWeight(brushSize);
  } else if (toolType=="eraser") {
    fill(backgroundColor);
    stroke(backgroundColor);
    strokeWeight(brushSize+15);
  }

  //DRAWING
  if (mousePressed && !(dontDrawForXFrames>0)) { //dontDrawForXFrames ensures user does not draw immediately after selecting settings, contracting menu, or in settings dashboard
    line(pmouseX,pmouseY,mouseX,mouseY);
  }
  //EXPANDED MENU BAR
  strokeWeight(1);
  stroke(0);

  if (!contractMenu) { //if expanded menu
    fill(255,230,162);
    rect(width-465,0,465,80); //toolbar background
    fill(0);
    text(">>",width-448,48); // >> button
    rect(width-385,0,2,80); //border between >> and tools
    image(toolType=="brush" ? brushBlue : brush,width-355,15); //brush icon
    image(toolType=="eraser" ? eraserBlue : eraser,width-278,15); //eraser icon
    image(framesAfterClear<80 ? clearBlue : clearImage,width-75,15);//clear is blue for 80 frames after selected

    
    //DROPDOWN MENU FOR COLOR & SIZE
    //BASE RECTANGLE (shows current settings)
    fill(255);
    rect(width-197,15,97,45); //base white rectangle
    fill(0);
    textSize(25);
    text(Math.round(brushSize/2),width-158,48); //brushSize text (÷2 so that it is not so big)
    fill(brushColor);
    rect(width-111,15,11,45); //brush color rectangle
    
    //EXPANDED RECTANGLE
    if (dropdownMenu) { //dropdownmenu OPEN
      fill(255);
      rect(width-197,60,97,240); //expanded menu white rectangle
      //COLOR OPTIONS
      fill(red);
      ellipse(width-176,279,23, 23); //small red circle
      fill(green);
      ellipse(width-148.5,279,23, 23); //small green circle
      fill(blue);
      ellipse(width-121,279,23, 23); //small blue circle
      //HIGHLIGHT ON HOVER
      if (mouseX>width-198 && mouseX<width-100 && mouseY>60) { //mouse is in expanded rectangle region
        fill(219,225,255); //light blue highlight color
        noStroke();
        if (mouseY<97) {
          rect(width-196,64,95,33); //-1 on both ends because black border
        } else if (mouseY<130) {
          rect(width-196,97,95,33);
        } else if (mouseY<163) {
          rect(width-196,130,95,33);
        } else if (mouseY<196) {
          rect(width-196,163,95,33);
        } else if (mouseY<229) {
          rect(width-196,196,95,33);
        } else if (mouseY<262) {
          rect(width-196,229,95,33);
        } else if (mouseX<width-164.6667) { //expanded red circle
          strokeWeight(1);
          stroke(0);
          fill(red);
          ellipse(width-176,279,30, 30);
        } else if (mouseX<width-132.333) { //expanded green circle
          strokeWeight(1);
          stroke(0);
          fill(green);
          ellipse(width-148.5,279,30, 30);
        } else if (mouseY<width-300) { //expanded blue circle
          strokeWeight(1);
          stroke(0);
          fill(blue);
          ellipse(width-121,279,30, 30);
        }
      }
      //SIZE OPTIONS
      strokeWeight(1);
      stroke(0);
      fill(0);
      triangle(width-182,45,width-175,33,width-168,45);
      text("6",width-155,90);
      text("9",width-155,123);
      text("11",width-164,156);
      text("14",width-164,189);
      text("18",width-164,222);
      text("22",width-164,255);
    } else { //dropdown menu CLOSED
      fill(0);
      triangle(width-182,33,width-175,45,width-168,33);
    }
    textSize(30);
    if (resetSettingsMenu) { //paints over old dropdown menu
      fill(backgroundColor);
      stroke(backgroundColor);
      rect(width-197,60,97,240); //rectangle of toolbar
    }
    //NOTIFICATION (informs user about keyboard shortcuts)
    if (framesAfterStart>3000) { //30 seconds pass since program start
      fill(255);
      if (framesAfterNotificationClosed<85) { //closes notification
        notificationX = width-2.375*(80-framesAfterNotificationClosed); //moves right every frame
      } else if (framesAfterStart-3000<80 && !notificationClosed) { //opens notification (for 0.8 sec)
        notificationX = width-2.375*(framesAfterStart-3000); //moves left every frame
      } else if (!notificationClosed) {
        notificationX = width-190;
      }
      fill(235); //gray background
      rect(notificationX,5,180,70,10);
      fill(0);
      textSize(15);
      text("Keyboard shortcuts:",notificationX+20,23);
      text("B - brush",notificationX+25,38.66);
      text("E - eraser",notificationX+25,54.33);
      text("R - clear",notificationX+25,70);
      //Closing [x]
      fill(250,34,39); //red color
      ellipse(notificationX+5,12,16, 16); //inside of notification rectangle
      fill(0);
      textSize(14);
      text("x",notificationX+1.2,16.5,10); 
      textSize(30);
    }
  } else { 
    //CONTRACTED MENU BAR
    if (resetMenu) { //covers up expanded menu bar
      fill(backgroundColor);
      stroke(backgroundColor);
      rect(width-465,0,465,80);
    }
    fill(255,230,162);
    stroke(0);
    rect(width-90,0,90,80);
    fill(0);
    text("<<",width-72,48); //expand button <<
  }
  //FRAME COUNTERS & TOGGLE VARIABLES
  clear = false;
  resetMenu = false;
  resetSettingsMenu = false;
  dontDrawForXFrames--;
  framesAfterStart++;
  framesAfterNotificationClosed++;
  framesAfterClear++;
}

void mousePressed() {
  if (!contractMenu) { 
    //EXPANDED MENU scenario
    if (mouseY<80 && mouseX>width-465) { 
      //MENU BAR (not contracted)
      dontDrawForXFrames=20; //doesn't draw when clicking in menu bar
      if (mouseX<width-385) { //contract >> selected
        contractMenu = true;
        resetMenu = true;
        dontDrawForXFrames = 10; //doesn't draw for 10 frames
      } if (mouseY>15 && mouseY<60) { //vertical position is in correct region
        //SELECTING TOOL
        if (mouseX>width-355 && mouseX<width-305) { //Brush selected
          toolType = "brush";
        } else if (mouseX>width-278 && mouseX<width-228) {//Eraser selected
          toolType = "eraser";
        } else if (mouseX>width-193 && mouseX<width-103) { 
          //OPEN/CLOSE DROPDOWN MENU
          if (dropdownMenu) { //if dropdown menu base doubleclicked
            //close menu
            dropdownMenu = false;
            resetSettingsMenu = true;
          } else { //if dropdown menu base singleclicked
            dropdownMenu = true; //open menu
          }
        } else if (mouseX>width-75 && mouseX<width-32) { //Clear selected
          clear = true;
          framesAfterClear=0;
        }
      }
    }
    //DROPDOWN MENU OPTION SELECTED
    if (dropdownMenu && mouseX>width-198 && mouseX<width-100 && mouseY>60 && framesAfterNotificationClosed>10) { //dropdown menu open & mouse in the expanded white rectangle
      //CHANGE COLOR/SIZE
      if (mouseY<97) {
        brushSize=6*2; //*2 because dropdown menu numbers scaled down to look normal
      } else if (mouseY<130) { //9
        brushSize=9*2;
      } else if (mouseY<163) { //11
        brushSize=11*2;
      } else if (mouseY<196) { //14
        brushSize=14*2;
      } else if (mouseY<229) { //18
        brushSize=18*2;
      } else if (mouseY<262) { //22
        brushSize=22*2;
      } else if (mouseX<width-164.6667) { //red circle selected
        brushColor=red;
        toolType = "brush"; //sets to brush because erasers don't have color
      } else if (mouseX<width-132.333) { //green circle selected
        brushColor=green;
        toolType = "brush"; //sets to brush because erasers don't have color
      } else if (mouseY<width-300) { //blue circle seleced
        brushColor=blue;
        toolType = "brush"; //sets to brush because erasers don't have color
      }
      //CLOSING DROPDOWN MENU PROCEDURES
      dropdownMenu = false; //stops painting dropdown menu
      resetSettingsMenu = true; //paints over old menu bar
      dontDrawForXFrames = 30; //mouse does not immediately draw when closing menu bar
    }
    //DISMISS NOTIFICATION [x]
    if (framesAfterStart>3080 && !notificationClosed && dist(mouseX,mouseY,notificationX+5,12)<=(16+1)/2) { //notification fully opened at 3080 frames & notification not previously closed & mouse is diameter+1 away from [x] center
      framesAfterNotificationClosed=0;
      notificationClosed=true;
    }
  } else { //CONTRACTED MENU scenario
    if (mouseX>width-80) { //if expand menu pressed <<
      contractMenu = false;
    }
  }
}

void keyPressed() {
  if (key=='b') {
    toolType = "brush";
  } else if (key=='e') {
    toolType = "eraser";
  } else if (key=='r') {
    clear = true;
    framesAfterClear=0;
  }
}
        </script>
        <canvas id="sketch" willReadFrequeytnly></canvas>
    </body>
</html>